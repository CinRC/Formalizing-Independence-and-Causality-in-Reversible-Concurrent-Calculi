%%% Definitions of the functions forget and enrich between CCSK and CCSKP transitions %%%
% They are defined as relations, and proved to be functions in the files `functionality.bel` and `totality.bel`.

% Function from forward closed CCSKP transitions to forward CCSK transitions.
LF forget_fc: ccskp.fstep_cl X T1 X' → ccsk.fstep X T2 X' → type =
  | fg_fc: {F1:ccskp.fstep_cl X T1 X'}{F2:ccsk.fstep X (ccsk.kl_base A K) X'}
           ccskp.lab T1 A → ccskp.key T1 K → forget_fc F1 F2
;

% Function from forward open CCSKP transitions to forward CCSK transitions.
LF forget_fo: ccskp.fstep_op X T1 X' → ccsk.fstep X T2 X' → type =
  | fg_fo: {F1:ccskp.fstep_op X (ccskp.open T1) X'}{F2:ccsk.fstep X (ccsk.kl_base tau K) X'}
           ({a:names} ccskp.key (T1 a) K) → forget_fo F1 F2
;

% Function from backward closed CCSKP transitions to backward CCSK transitions.
LF forget_bc: ccskp.bstep_cl X' T1 X → ccsk.bstep X' T2 X → type =
  | fg_bc: {B1:ccskp.bstep_cl X' T1 X}{B2:ccsk.bstep X' (ccsk.kl_base A K) X}
           ccskp.lab T1 A → ccskp.key T1 K → forget_bc B1 B2
;

% Function from backward open CCSKP transitions to backward CCSK transitions.
LF forget_bo: ccskp.bstep_op X' T1 X → ccsk.bstep X' T2 X → type =
  | fg_bo: {B1:ccskp.bstep_op X' (ccskp.open T1) X}{B2:ccsk.bstep X' (ccsk.kl_base tau K) X}
           ({a:names} ccskp.key (T1 a) K) → forget_bo B1 B2
;

% Function from forward CCSK transitions to forward closed CCSKP transitions.
LF enrich_fc: ccsk.fstep X T1 X' → ccskp.fstep_cl X T2 X' → type =
  | en_fc: {F1:ccsk.fstep X (ccsk.kl_base A K) X'}{F2:ccskp.fstep_cl X T2 X'}
           ccskp.lab T2 A → ccskp.key T2 K → enrich_fc F1 F2
;

% Function from forward CCSK transitions to forward open CCSKP transitions.
LF enrich_fo: ccsk.fstep X T1 X' → ccskp.fstep_op X T2 X' → type =
  | en_fo: {F1:ccsk.fstep X (ccsk.kl_base tau K) X'}{F2:ccskp.fstep_op X (ccskp.open T2) X'}
           ({a:names} ccskp.key (T2 a) K) → enrich_fo F1 F2
;

% Function from backward CCSK transitions to backward closed CCSKP transitions.
LF enrich_bc: ccsk.bstep X' T1 X → ccskp.bstep_cl X' T2 X → type =
  | en_bc: {B1:ccsk.bstep X' (ccsk.kl_base A K) X}{B2:ccskp.bstep_cl X' T2 X}
           ccskp.lab T2 A → ccskp.key T2 K → enrich_bc B1 B2
;

% Function from backward CCSK transitions to backward open CCSKP transitions.
LF enrich_bo: ccsk.bstep X' T1 X → ccskp.bstep_op X' T2 X → type =
  | en_bo: {B1:ccsk.bstep X' (ccsk.kl_base tau K) X}{B2:ccskp.bstep_op X' (ccskp.open T2) X}
           ({a:names} ccskp.key (T2 a) K) → enrich_bo B1 B2
;


% Function from combined CCSKP transitions to combined CCSK transitions.
LF forget: ccskp.step X T1 X' → ccsk.step X T2 X' → type =
  | fgfc: forget_fc F1 F2 → forget (ccskp.sc (ccskp.fwc F1)) (ccsk.fw F2)
  | fgfo: forget_fo F1 F2 → forget (ccskp.so (ccskp.fwo F1)) (ccsk.fw F2)
  | fgbc: forget_bc B1 B2 → forget (ccskp.sc (ccskp.bwc B1)) (ccsk.bw B2)
  | fgbo: forget_bo B1 B2 → forget (ccskp.so (ccskp.bwo B1)) (ccsk.bw B2)
;

% Function from combined CCSK transitions to combined CCSKP transitions.
LF enrich: ccsk.step X T1 X' → ccskp.step X T2 X' → type =
  | enfc: enrich_fc F1 F2 → enrich (ccsk.fw F1) (ccskp.sc (ccskp.fwc F2))
  | enfo: enrich_fo F1 F2 → enrich (ccsk.fw F1) (ccskp.so (ccskp.fwo F2))
  | enbc: enrich_bc B1 B2 → enrich (ccsk.bw B1) (ccskp.sc (ccskp.bwc B2))
  | enbo: enrich_bo B1 B2 → enrich (ccsk.bw B1) (ccskp.so (ccskp.bwo B2))
;